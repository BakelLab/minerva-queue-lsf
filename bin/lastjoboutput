#!/usr/bin/perl

# Shows the output of the last N jobs that finished running on the cluster
# Convenience function to avoid having to find the most recent job output file

use strict;
use warnings;
use Getopt::Long;

# GLOBALS
$ENV{SJOB_OUTPUT}  ||= "$ENV{HOME}/lsf-output";    # Folder with pbs output files

# ARGUMENTS
my $sHelp       = 0;
my $nFileCount  = 1;
my $nSortByID   = 0;
my $sAltPath    = "";
GetOptions("help!"  => \$sHelp,
           "i!"     => \$nSortByID,
           "n:i"    => \$nFileCount,
           "p:s"    => \$sAltPath);
           
# PRINT HELP
my $sScriptName = ($0 =~ /^.*\/(.+$)/) ? $1 : $0;
if ($sHelp) {
    die <<HELP

    $sScriptName [ -n | -i ]

    Shows the content of the pbs output file for the most recently finished job(s).
    
    Options:
    -n <integer>
      Show output of the last n files, instead of just the last one
    -p <string>
      Specify an alternative path to the job output folder
    -i
      Use job ID rather than file modification time to select most recent jobs
    -help
      This help message
      
HELP
}

# START

# Set output path
my $sJobOutputPath = $sAltPath ? $sAltPath : $ENV{SJOB_OUTPUT};
die "Error: Job output directory does not exist\n" unless (-d $sJobOutputPath);

# Sort the output files by job ID or modification date
opendir my($dir), $sJobOutputPath or die "Can't open $sJobOutputPath : $!\n";
my @asFiles = grep { -f "$sJobOutputPath/$_" } readdir $dir;
if ($nSortByID){
   my @anJobIDs = @asFiles;
   for my $nID (@anJobIDs){
      $nID =~ s/\..*//; # strip extension to only leave job ID
   }
   my @anSortOrder = (0 .. scalar(@asFiles)-1);
   @anSortOrder    = sort {$anJobIDs[$b] <=> $anJobIDs[$a]} @anSortOrder;
   @asFiles = @asFiles[@anSortOrder];
}
else{
   @asFiles = sort { -M "$sJobOutputPath/$a" <=> -M "$sJobOutputPath/$b" } @asFiles;
}



# Print the sorted list of job outputs
$nFileCount = scalar(@asFiles) if (scalar(@asFiles)<$nFileCount);
die "No job output files found\n" unless($nFileCount);
for (my $i=$nFileCount-1; $i>=0; $i--){
   my $time = localtime((stat("$sJobOutputPath/$asFiles[$i]"))[9]);
   print "\n## $time #################################################################\n";
   print "==> $asFiles[$i]\n";
   open OUT, "$sJobOutputPath/$asFiles[$i]" or die "Can't open $sJobOutputPath/$asFiles[$i]: $!\n";
   while (<OUT>){print;}
   close OUT;
   print "\n"
}
